{
  description = "Fixed flake for NixOS + home-manager + NVF";

  inputs = {
    # primary channels
    nixpkgs.url = "github:NixOS/nixpkgs/nixos-26.05";
    nixpkgs-unstable.url = "github:NixOS/nixpkgs/nixos-unstable";

    # home-manager
    home-manager.url = "github:nix-community/home-manager/release-26.05";
    home-manager.inputs.nixpkgs.follows = "nixpkgs";

    # NVF (Neovim framework)
    nvf.url = "github:notashelf/nvf";
    nvf.inputs.nixpkgs.follows = "nixpkgs-unstable";

    # Noctalia shell (Wayland desktop shell + launcher)
    noctalia.url = "github:noctalia-dev/noctalia-shell";
    noctalia.inputs.nixpkgs.follows = "nixpkgs-unstable";

    # Star Citizen helper flake
    #nix-citizen.url = "github:LovingMelody/nix-citizen";

    # Herdr terminal workflow
    herdr.url = "github:ogulcancelik/herdr/v0.7.1";

    # Neorg flashcards plugin and NVF module
    luixbits-neorg-flashcards.url = "github:LuixBits/luixbits-neorg-flashcards.nvim";

    # Sentry plugin and NVF module
    luixbits-sentry.url = "github:LuixBits/luixbits-sentry.nvim";

    # RoomPlan plugin
    roomplan.url = "github:LuixBits/luixbits-roomplanner.nvim";
    roomplan.inputs.nixpkgs.follows = "nixpkgs";

    # Neotest adapter for Node's built-in test runner. This is kept as a raw
    # source input because it is not packaged in our pinned nixpkgs yet.
    neotest-nodejs = {
      url = "github:AkisArou/neotest-nodejs";
      flake = false;
    };

  };

  outputs =
    { nixpkgs, home-manager, ... }@inputs:
    let
      mkHost =
        {
          hostName,
          homeHost,
          hmUser,
        }:
        nixpkgs.lib.nixosSystem {
          system = "x86_64-linux";
          specialArgs = { inherit inputs; };

          modules = [
            ./hosts/${hostName}

            # Home-Manager as a NixOS module
            home-manager.nixosModules.home-manager
            {
              home-manager.useUserPackages = true;
              home-manager.backupFileExtension = "hm-back";
              home-manager.overwriteBackup = true;
              home-manager.extraSpecialArgs = {
                inherit inputs hostName;
              };
              home-manager.users = {
                "${hmUser}" = import homeHost;
              };
            }
          ];
        };
    in
    {
      nixosConfigurations = {
        osgiliath = mkHost {
          hostName = "osgiliath";
          homeHost = ./home/hosts/osgiliath.nix;
          hmUser = "tillo";
        };
      };
    };
}
