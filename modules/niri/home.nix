{ config, pkgs, lib, hostName ? null, ... }:
let
  isWorkProfile = hostName == "work" || (hostName == null && config.home.username == "luiz");
  isLaptopProfile = hostName == "l";
  isPcProfile = hostName == "pc";
  isOsgiliathProfile = hostName == "osgiliath";
  workLaptopOutput = "eDP-1";
  laptopInternalOutput = "eDP-1";
  sharedMainOutput = "PNP(BNQ) BenQ EX3415R R7M0014701Q";
  sharedRightPortraitOutput = "LG Electronics LG HDR 4K 405NTQDBG628";
  # Match external displays by make/model/serial so DisplayLink connector order
  # changes do not break rotation/placement.
  workMainOutput = sharedMainOutput;
  workRightPortraitOutput = sharedRightPortraitOutput;
  pcMainOutput = sharedMainOutput;
  pcRightPortraitOutput = sharedRightPortraitOutput;
  # Same reasoning: connector order (DP-2/DP-3) is not stable across boots on
  # this host, which caused the two identical Lenovo monitors to swap sides.
  osgiliathLeftOutput = "Lenovo Group Limited LEN P24h-20 V3066412";
  osgiliathRightOutput = "Lenovo Group Limited LEN P24h-20 V306640T";
  outputConfig =
    if isLaptopProfile then
      ''
        output "${laptopInternalOutput}" {
            mode "2880x1800@120.000"
            scale 1.75
            position x=0 y=0
        }

        output "${sharedMainOutput}" {
            position x=1646 y=0
            focus-at-startup
        }

        output "${sharedRightPortraitOutput}" {
            scale 1.25
            transform "270"
            position x=5086 y=0
        }
      ''
    else if isWorkProfile then
      ''
        output "${workLaptopOutput}" {
            position x=0 y=0
        }

        output "${workMainOutput}" {
            position x=1600 y=0
            focus-at-startup
        }

        output "${workRightPortraitOutput}" {
            transform "270"
            position x=5040 y=0
        }
      ''
    else if isOsgiliathProfile then
      ''
        output "${osgiliathLeftOutput}" {
            position x=0 y=0
            focus-at-startup
        }

        output "${osgiliathRightOutput}" {
            position x=2560 y=0
        }
      ''
    else if isPcProfile then
      ''
        output "${pcMainOutput}" {
            mode "3440x1440@100.000"
            position x=0 y=0
            focus-at-startup
        }

        output "${pcRightPortraitOutput}" {
            mode "3840x2160@59.997"
            scale 1.25
            transform "270"
            position x=3440 y=0
        }
      ''
    else
      ''
        output "HDMI-A-2" {
            mode "3440x1440@100.000"
            position x=0 y=0
            focus-at-startup
        }

        output "HDMI-A-3" {
            mode "3840x2160@59.997"
            scale 1.25
            transform "270"
            position x=3440 y=0
        }
      '';
  workRenderConfig =
    if isWorkProfile then
      ''
        // Keep work on the Intel render node: this is the only path that
        // consistently brings both DisplayLink outputs up.
        debug {
            render-drm-device "/dev/dri/by-path/pci-0000:00:02.0-render"
        }
      ''
    else
      "";
  baseConfig = builtins.readFile "${pkgs.niri.doc}/share/doc/niri/default-config.kdl";
  noWaybarConfig = lib.replaceStrings [
    "spawn-at-startup \"waybar\"\n"
  ] [
    "spawn-at-startup \"noctalia\"\n"
  ] baseConfig;
  noCommaConfig = lib.replaceStrings [
    "    Mod+Comma  { consume-window-into-column; }\n"
  ] [
    ""
  ] noWaybarConfig;
  widthConfig = lib.replaceStrings [
    "    Mod+Period { expel-window-from-column; }\n"
  ] [
    "    Mod+Period { set-column-width \"+10%\"; }\n"
  ] noCommaConfig;
  noctaliaLauncherBinds = ''
    Mod+Space hotkey-overlay-title="Noctalia: Launcher" { spawn "noctalia-ipc" "panel-toggle" "launcher"; }
    Mod+D hotkey-overlay-title="Noctalia: Launcher" { spawn "noctalia-ipc" "panel-toggle" "launcher"; }
    Mod+S hotkey-overlay-title="Noctalia: Control Center" { spawn "noctalia-ipc" "panel-toggle" "control-center"; }
    Mod+Comma hotkey-overlay-title="Noctalia: Settings" { spawn "noctalia-ipc" "settings-toggle"; }
    Mod+F1 { spawn "noctalia" "msg" "panel-toggle" "kenn/keybind-cheatsheet:cheatsheet"; }
  '';
  noctaliaLockBind = ''
    Super+Alt+L hotkey-overlay-title="Noctalia: Lock" { spawn "noctalia-ipc" "session" "lock"; }
  '';
  noFuzzelConfig = lib.replaceStrings [
    "    Mod+D hotkey-overlay-title=\"Run an Application: fuzzel\" { spawn \"fuzzel\"; }\n"
  ] [
    "    ${noctaliaLauncherBinds}\n"
  ] widthConfig;
  noctaliaConfig = lib.replaceStrings [
    "    Super+Alt+L hotkey-overlay-title=\"Lock the Screen: swaylock\" { spawn \"swaylock\"; }\n"
  ] [
    "    ${noctaliaLockBind}\n"
  ] noFuzzelConfig;
  kittyTerminalConfig = lib.replaceStrings [
    "    Mod+T hotkey-overlay-title=\"Open a Terminal: alacritty\" { spawn \"alacritty\"; }\n"
  ] [
    "    Mod+T hotkey-overlay-title=\"Open a Terminal: kitty\" { spawn \"kitty\"; }\n"
  ] noctaliaConfig;
  noBrightnessConfig = lib.replaceStrings [
    "    XF86MonBrightnessUp allow-when-locked=true { spawn \"brightnessctl\" \"--class=backlight\" \"set\" \"+10%\"; }\n"
    "    XF86MonBrightnessDown allow-when-locked=true { spawn \"brightnessctl\" \"--class=backlight\" \"set\" \"10%-\"; }\n"
  ] [
    ""
    ""
  ] kittyTerminalConfig;
in
{
  imports = [
    ./noctalia/home.nix
    ./polkit/home.nix
  ];

  xdg.configFile."niri/config.kdl".text =
    noBrightnessConfig
    + ''
      ${outputConfig}
      ${workRenderConfig}
    '';
}
